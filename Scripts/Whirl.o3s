// t parameter
// min and initial value
0
?t

// max value = 12 * pi
3.14159
20
*
?max_t

// t increment
0.00625
?t_step

// factorized parametric equation

// fun(x) = sin(t) * t
fundef x_func_of_t
    !t
    math_call sin
    !t
    *
end_fundef

// fun(y) = cos(t) * t
fundef y_func_of_t
    !t
    math_call cos
    !t
    *
end_fundef

// erase background
0
2
dupn
erase

// wirl color
255
2
dupn
color

// drawing range : -100 < x < 100, -100 < y < 100
-100
100
-100
100
range

// pen size
5
dot_size

// iterate until t reaches max_t, by increments of t_step
1
while
    drop
    funcall x_func_of_t
    funcall y_func_of_t
    plot
    !t
    !t_step
    +
    dup
    ?t
    !max_t
    funcall lte
end_while
drop

// get rid of local functions
fundel x_func_of_t
fundel y_func_of_t

// display message to the user
!"done drawing whirl
