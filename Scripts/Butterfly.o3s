// t parameter
// min and initial value
0
?t

// max value = 12 * pi
3.14159
12
*
?max_t

// t increment
0.00625
?t_step

// factorized parametric equation
// fun(t) = (e(cos(t)) - (cos(4 * t) * 2) - sin(t / 12))
fundef func_of_t
    !t
    math_call cos
    math_call exp
    !t
    4
    *
    math_call cos
    2
    *
    -
    !t
    12
    /
    math_call sin
    -
end_fundef

// fun(x) = sin(t) * fun(t)
fundef x_func_of_t
    funcall func_of_t
    !t
    math_call sin
    *
end_fundef

// fun(y) = cos(t) * fun(t)
fundef y_func_of_t
    funcall func_of_t
    !t
    math_call cos
    *
end_fundef

// erase background
0
2
dupn
erase

// butterfly color
0
6
dupn
?r
70
+
?g
?b
color

// drawing range : -5 < x < 5, -5 < y < 5
-5
5
-5
5
range

// pen size
5
dot_size

// iterate until t reaches max_t, by increments of t_step
1
while
    drop
    funcall x_func_of_t
    dup
    math_call abs
    50
    *
    ?r
    funcall y_func_of_t
    dup
    math_call abs
    50
    *
    ?b
    plot
    !r
    !g
    !b
    color
    !t
    !t_step
    +
    dup
    ?t
    !max_t
    funcall lte
end_while
drop

// get rid of local functions
fundel x_func_of_t
fundel y_func_of_t
fundel func_of_t

// display message to the user
!"done drawing butterfly
